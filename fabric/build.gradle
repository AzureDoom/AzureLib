import groovy.json.JsonOutput
import groovy.json.JsonSlurper

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'fabric-loom'
    id "me.modmuss50.mod-publish-plugin" version "0.4.0"
}

base {
    archivesName = "${mod_id}-fabric-${minecraft_version}"
}

repositories {
    mavenCentral()
    maven { url 'https://dl.cloudsmith.io/public/azuredoom-mods/azurelib/maven/' }
    maven { url 'https://maven.blamejared.com' }
    maven { url 'https://api.modrinth.com/maven' }
    maven { url "https://cfa2.cursemaven.com" }
    maven { url "https://maven.cloudsmith.io/azuredoom-mods/azurelib/" }
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/" }
    maven { url "https://maven.terraformersmc.com/releases" } // modmenu
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    modCompileOnly "com.terraformersmc:modmenu:7.2.1"
    compileOnly project(":common")
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.aw").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.aw"))
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}

processResources {
    from project(":common").sourceSets.main.resources
    doLast {
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each {
            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }
        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}

// TODO: Everything below here might be fine to move to the top-level build.gradle file.
// They were originally in the previous fabric build.gradle. - bvanseg

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

jar {
    from "LICENSE"
}

publishing {
    repositories {
        maven {
            name = "azurelib"
            url = "https://libs.azuredoom.com:4443/mods"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
}

if (file('key.properties').exists()) {
    publishMods {
        def releaseProp = new Properties()
        File secretPropsFile = file("key.properties")
        releaseProp.load(secretPropsFile.newInputStream())
        file = remapJar.archiveFile
        changelog = rootProject.file("changelog.txt").text
        type = STABLE
        modLoaders.add("fabric")

        curseforge {
            projectId = "817423"
            projectSlug = "azurelib"
            accessToken = releaseProp.getProperty("curseKey")
            minecraftVersions.add("1.20.1")
            requires {
                slug = "fabric-api"
            }
        }
        modrinth {
            projectId = "7zlUOZvb"
            accessToken = releaseProp.getProperty('modrinthKey')
            minecraftVersions.add("1.20.1")
        }
    }
}